defaultTasks 'clean', 'test', 'assemble', 'createReadme'

allprojects {
  apply plugin: 'groovy'
  apply plugin: 'eclipse'
  sourceCompatibility = 1.6
  targetCompatibility = 1.6
  compileJava.options.fork([memoryMaximumSize: '1024m'])

  group = 'org.climatic'
  
  repositories {
      mavenCentral()
  }
  
  dependencies {
      groovy 'org.codehaus.groovy:groovy-all:2.0.5'
      testCompile 'junit:junit:4.7',
                  'org.spockframework:spock-core:0.7-groovy-2.0'
  }
}

dependencies {
    runtime 'commons-cli:commons-cli:1.2'
}

project(':samples-todo-list') {
  dependencies {
    compile rootProject
  }
}

task createWrapper(type:Wrapper) {
    gradleVersion = '1.5'
}

task createReadme << {
    new org.climatic.gradle.readme.Readme().generate([jar.archivePath] + configurations.runtime.files)
}

createReadme.dependsOn 'assemble'
